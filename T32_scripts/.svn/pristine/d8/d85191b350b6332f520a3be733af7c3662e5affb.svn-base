; by Ken.Kuang
LOCAL &AHB_PORT &TargetID
ENTRY &AHB_PORT &TargetID

LOCAL &CUR_DIR
&CUR_DIR=OS.PPD()

IF ("&os_sel"!="os_sys")
(
	LOCAL &save_os_sel
	&save_os_sel="&os_sel"
	&os_sel="os_sys"
	CD.DO "&CUR_DIR\CM4.cmm"
	&os_sel="&save_os_sel"
	GOSUB BOOT
	WAIT 500.MS
)

IF "X&target"=="X"
(
	&target="CHIP"
	;&target="FPGA"
	;&target="PXP"
)

IF "X&AHB_PORT"=="X"
(
	&AHB_PORT=0.
)

IF "X&TargetID"=="X"
(
	&TargetID=(0x2<<28.)|0x1
)

IF "X&os_sel"=="X"
(
	&os_sel="small_sys"
)

IF (CPUFAMILY()!="ARM")
(
	PRINT %ERROR "Please open the right Trace32!"
	ENDDO
)



SYStem.Mode Down
SYStem.RESet
ON ERROR CONTinue
IF "&target"=="FPGA"
	SYStem.JtagClock 5MHz
ELSE IF "&target"=="PXP"
	SYStem.JtagClock 150KHz
ELSE IF "&target"=="Protium"
	SYStem.JtagClock 500KHz
ELSE
	SYStem.JtagClock 20MHz
ON ERROR

SYStem.CPU CORTEXM4

SYStem.CONFIG MEMORYACCESSPORT &AHB_PORT
SYStem.CONFIG DEBUGACCESSPORT 0xFF ;1.

SYStem.CONFIG AXIACCESSPORT   0xFF ;0.
SYStem.CONFIG APBACCESSPORT   0xFF ;1.
SYStem.CONFIG AHBACCESSPORT   &AHB_PORT
SYStem.MemAccess.DAP

ON ERROR CONTinue
SYStem.CONFIG SWDP ON
ON ERROR
SYStem.CONFIG SWDPTargetSel &TargetID
SYStem.Mode.Prepare

IF ("&os_sel"=="os_sys")
(

	WAIT 1.MS
	PRINT "Trace32 Prepared!"
	;SYStem.Mode.Attach
)
ELSE
(
	SYStem.Mode Attach
	Break
)

ENDDO

BOOT:
	DO "&CUR_DIR\boot_all.cmm" eSE_BOOT 1
	RETURN
; end of BOOT

ERROR_Handle:
	ON ERROR
	IF STATE.POWER()==FALSE()
	(
		PRINT %ERROR "Please Power up device, and then, try it again!"
	)
	ELSE
	(
		PRINT %ERROR "Maybe Hardware Error!"
	)
	ENDDO
